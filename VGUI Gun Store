@name VGUI-Store
@persist [E O]:entity
@persist [GridXY RGB Visible] Model:string Grid:array
@persist [Selected Markup Profit ShipsSold Inventory] [Prefix Title]:string Guns:array Lookup:table
@persist [SettingsFrame StoreFrame]:dframe Dropdown:dcombobox [Buy Add]:dbutton Mdl:dmodelpanel [Stats List]:dlistview

if(first()){
    # |--- Grid Vars ---|
    GridXY  = 5
    Holos   = 1
    RGB     = 1
    Model   = "models/props/de_nuke/crate_extrasmall.mdl"

    Markup = 10000
    Title  = "Store"
    Prefix = "/"

    # |--- Creation of Grid ---|
    for(I = 1, GridXY * GridXY){
        Grid:pushVector(entity():pos() + vec(floor((I - 1) / GridXY) * 40, ((I - 1) % GridXY) * 40, 0))
        if(Holos){
            holoCreate(I, entity():pos() + vec(floor((I - 1) / GridXY) * 40, ((I - 1) % GridXY) * 40, 0), vec(0), ang(), vec4(255, 255, 255, 100), Model)
            holoScaleUnits(I, vec(32, 32, 24))
            if(RGB){ holoColor(I, hsv2rgb((curtime() * 60 + (I * 5)) % 360, 1, 1)) }
        }
    }
    
    # |--- Shorthand Vars ---|
    E = entity()
    O = owner()
    
    # |--- Triggers ---|
    vguiDefaultPlayers(players())
    runOnVgui(1)
    runOnChat(1)
    
    # |--- Store Frame ---|
    StoreFrame = dframe(1)
    StoreFrame:center()
    StoreFrame:makePopup()
    StoreFrame:setTitle(Title)
    StoreFrame:setDeleteOnClose(0)
    StoreFrame:setColor(vec(50))
    StoreFrame:setVisible(0)
    StoreFrame:setSize(576, 540)
    StoreFrame:create()
    
    # |--- Settings Frame ---|
    SettingsFrame = dframe(2)
    SettingsFrame:center()
    SettingsFrame:makePopup()
    SettingsFrame:setTitle(Title + " Admin")
    SettingsFrame:setDeleteOnClose(0)
    SettingsFrame:setColor(vec(50))
    SettingsFrame:setVisible(0)
    SettingsFrame:setSize(576, 540)
    SettingsFrame:create()
    
    # |--- Store ---|
    Dropdown = dcombobox(3, StoreFrame)
    Dropdown:setSize(100, 25)
    Dropdown:dockMargin(10, 10, 10, 0)
    Dropdown:dock(_DOCK_TOP)
    Dropdown:setText("Select a Weapon")
    Dropdown:create()

    Buy = dbutton(4, StoreFrame)
    Buy:setSize(100, 25)
    Buy:dockMargin(135, 10, 135, 0)
    Buy:setColor(vec(0, 150, 230))
    Buy:setTextColor(vec(255), vec(255), vec(255), vec(255))
    Buy:dock(_DOCK_TOP)
    Buy:setText("Purchase")
    Buy:create()

    Mdl = dmodelpanel(5, StoreFrame)
    Mdl:dockMargin(10, 10, 10, 10)
    Mdl:dock(_DOCK_FILL)
    Mdl:setRotateModel(0)
    Mdl:create()
    
    # |--- Settings ---|
    List = dlistview(6, SettingsFrame)
    List:dockMargin(10, 10, 10, 0)
    List:dock(_DOCK_FILL)
    List:addColumn("Name")
    List:addColumn("Price")
    List:addColumn("Amount")
    List:create()
    
    Add = dbutton(7, SettingsFrame)
    Add:dockMargin(10, 10, 10, 10)
    Add:dock(_DOCK_BOTTOM)
    Add:setColor(vec(0, 150, 230))
    Add:setTextColor(vec(255), vec(255), vec(255), vec(255))
    Add:setText("Add")
    Add:create()
    
    Stats = dlistview(8, SettingsFrame)
    Stats:setHeight(35)
    Stats:addColumn("Profit")
    Stats:addColumn("Shipments Sold")
    Stats:addColumn("Inventory")
    Stats:addLine(Profit, ShipsSold, Inventory)
    Stats:dockMargin(10, 10, 10, 0)
    Stats:dock(_DOCK_BOTTOM)
    Stats:create()
    
    # |--- Functions ---|
    function array fancychat(Err, Str:string){
        local Col = Err ? vec(0, 255, 0) : vec(255, 0, 0)
        return array(Col, "| ", Str)
    }
    
    function number entity:cost(){
        return This:shipmentPricePerUnit() * This:shipmentAmount() + Markup
    }
    
    function listUpdate(){
        List:clear()
        Stats:clear()
        Dropdown:clear()
        foreach(K, V:entity = Guns){
            if(V:getOwningEnt() == O){
                Dropdown:addChoice(format("Name: %s | Amount: %s | Price: $%s", V:shipmentName(), V:shipmentAmount(), V:cost()), K)
                List:addLine(V:shipmentName(), V:cost(), V:shipmentAmount())
            }
        }
        Dropdown:modify()
        List:modify()
        
        Stats:addLine(Profit, ShipsSold, Inventory)
        Stats:modify()
    }
    
    function findShipments(){
        findByClass("spawned_shipment")
        Guns = findToArray()
        Inventory = Guns:count()
        
        for(I = 1, Grid:count()){
            if(Guns[I, entity]:getOwningEnt() == O){
                Guns[I, entity]:setPos(Grid[I, vector])
                Guns[I, entity]:setAng(ang(E:forward()))
            }   
        }
        listUpdate()
    }
    
    # |--- Run Functions ---|
    findShipments()
    
    # |--- Lookup Table for Weapon Models ---|
    # |- FOV, CamPos, LookAt -|
    Lookup = table(
        "models/weapons/w_dmg_glock.mdl" = array(10, vec(90, 0, 0), vec(0, 0, -4)),
        "models/weapons/s_dmgf_co1911.mdl" = array(10, vec(90, 0, 0), vec(0, 0, -4)),
        "models/weapons/w_colt_python.mdl" = array(10, vec(-90, 0, 0), vec(0, -7, 0)),
        "models/weapons/w_tcom_deagle.mdl" = array(10, vec(90, 0, 0), vec(0, 0, -2)),
        "models/weapons/w_pist_fokkususp.mdl" = array(4, vec(0, 200, 0), vec(0, 0, -3)),
        "models/weapons/w_hk45c.mdl" = array(7, vec(-90, 0, 0), vec(0, -5, 1)),
        "models/weapons/w_beretta_m92.mdl" = array(8, vec(-90, 0, 0), vec(0, -6, 1)),
        "models/weapons/w_luger_p08.mdl" = array(8, vec(-90, 0, 0), vec(0, -6, 1)),
        "models/weapons/w_taurus_raging_bull.mdl" = array(10, vec(-90, 0, 0), vec(0, -8.5, 2)),
        "models/weapons/w_sig_229r.mdl" = array(10, vec(-90, 0, 0), vec(0, -5, 2)),        
        "models/weapons/w_masada_acr.mdl" = array(25, vec(0, 90, 0), vec(10, 0, 0)),
        "models/weapons/w_ak47_m9k.mdl" = array(25, vec(0, 90, 0), vec(10, 0, 0)),
        "models/weapons/w_dmg_vally.mdl" = array(28, vec(90, 0, 0), vec(10, 0, -5)),
        "models/weapons/w_fn_f2000.mdl" = array(25, vec(0, 90, 0), vec(7, 0, 0)),
        "models/weapons/w_hk_416.mdl" = array(25, vec(0, 90, 0), vec(9, 0, 0)),
        "models/weapons/w_snip_m14sp.mdl" = array(30, vec(0, 90, 0), vec(0, 0, 0)),
        "models/weapons/w_m4a1_iron.mdl" = array(27, vec(0, 90, 0), vec(4, 0, 0)),
        "models/weapons/w_fn_scar_h.mdl" = array(25, vec(0, 90, 0), vec(8, 0, 0)),        
        "models/weapons/w_ares_shrike.mdl" = array(25, vec(0, 90, 0), vec(10, 0, 0)),
        "models/weapons/w_fg42.mdl" = array(25, vec(0, 90, 0), vec(10, 0, 0)),
        "models/weapons/w_m1918_bar.mdl" = array(25, vec(0, 90, 0), vec(12, 0, 0)),
        "models/weapons/w_m249_machine_gun.mdl" = array(27, vec(0, 90, 0), vec(7, 0, 0)),
        "models/weapons/w_mach_russ_pkm.mdl" = array(28, vec(0, 90, 0), vec(3, 0, 0)),
        "models/weapons/w_m60_machine_gun.mdl" = array(30, vec(0, 90, 0), vec(12, 0, 0)),        
        "models/weapons/w_benelli_m3.mdl" = array(25, vec(0, 90, 0), vec(10, 0, 0)),
        "models/weapons/w_browning_auto.mdl" = array(30, vec(0, 90, 0), vec(14, 0, 0)),
        "models/weapons/w_mossberg_590.mdl" = array(27, vec(0, 90, 0), vec(10, 0, 0)),
        "models/weapons/w_pancor_jackhammer.mdl" = array(20, vec(0, 90, 0), vec(5, 0, 0)),
        "models/weapons/w_remington_870_tact.mdl" = array(33, vec(0, 90, 0), vec(14, 0, 0)),
        "models/weapons/w_usas_12.mdl" = array(25, vec(0, 90, 0), vec(8, 0, 0)),        
        "models/weapons/w_acc_int_aw50.mdl" = array(35, vec(0, 90, 0), vec(13, 0, 0)),
        "models/weapons/w_barret_m82.mdl" = array(30, vec(0, 90, 0), vec(17, 0, 0)),
        "models/weapons/w_barrett_m98b.mdl" = array(30, vec(0, 90, 0), vec(17, 0, 0)),
        "models/weapons/w_snip_int.mdl" = array(31, vec(90, 0, 0), vec(0, 7, 0)),
        "models/weapons/w_snip_m24_6.mdl" = array(31, vec(90, 0, 0), vec(0, 7, 0)),        
        "models/weapons/w_ex41.mdl" = array(23, vec(0, 90, 0), vec(12, 0, 0)),
        "models/weapons/w_m61_fraggynade.mdl" = array(5, vec(0, 90, 0), vec(0, 0, -0.5)),
        "models/weapons/w_m79_grenadelauncher.mdl" = array(27, vec(0, 90, 0), vec(8, 0, 0)),
        "models/weapons/w_sticky_grenade.mdl" = array(5, vec(0, 90, 0), vec(13, 0, 0)),
        "models/weapons/w_sb.mdl" = array(5, vec(0, 90, 200), vec(0, 0, 0)),
        "models/weapons/w_aac_honeybadger.mdl" = array(21, vec(0, 90, 0), vec(8, 0, 0)),
        "models/weapons/w_fn_p90.mdl" = array(15, vec(0, 90, 0), vec(2, 0, 0)),
        "models/weapons/w_smg_mp5.mdl" = array(22, vec(0, 90, 0), vec(8, 0, 0)),
        "models/weapons/w_hk_ump45.mdl" = array(22, vec(0, 90, 0), vec(4, 0, 0)),          
        "models/weapons/w_hk_mp5sd.mdl" = array(22, vec(0, 90, 0), vec(10, 0, 0)),      
        "models/weapons/w_brugger_thomet_mp9.mdl" = array(22, vec(0, 90, 0), vec(4, 0, 0)),
        "models/weapons/w_uzi_imi.mdl" = array(20, vec(0, 90, 0), vec(1, 0, 0))
    )
}

# |--- Chat Commands ---|
if(chatClk() & lastSaid():sub(1, Prefix:length()) == Prefix){
    hideChat(1) 

    S = lastSaid():explode(" ")
    switch(S[1, string]:sub(Prefix:length() + 1)){
        # |--- Store related Commands ---|
        case "store", 
            local Visible = StoreFrame:isVisible(lastSpoke())
            StoreFrame:setVisible(!Visible)
            StoreFrame:modify(array(lastSpoke()))
            
            fancychat(1, format("\"%s\" has opened the store menu", lastSpoke():name()))
        break
        case "admin",
            local Visible = SettingsFrame:isVisible(lastSpoke())
            SettingsFrame:setVisible(!Visible)
            SettingsFrame:modify(array(lastSpoke()))
            
            fancychat(1, format("\"%s\" has opened the admin menu", lastSpoke():name()))
        break
    }
}

# |--- VGUI Functionality ---|
if(vguiClk()){
    local Ply = vguiClkPlayer()
    
    switch(vguiClkPanelID()){
        # |--- Store Functionality ---|
        # |- Dropdown -|
        case 3,
            Selected = Dropdown:getData(Ply):toNumber()
            local Model = Guns[Selected, entity]:shipmentModel()
            Mdl:setModel(Model)
            
            Mdl:setFOV(Lookup[Model, array][1, number])
            Mdl:setCamPos(Lookup[Model, array][2, vector])
            Mdl:setLookAt(Lookup[Model, array][3, vector])
            
            Mdl:modify(array(Ply))
        break      
        # |- Purchase Button -|
        case 4,
            Selected = Dropdown:getData(Ply):toNumber()
            if(Selected != 0){
                moneyRequest(Ply, Guns[Selected, entity]:cost())
            }
            
            Mdl:setModel("")
            Mdl:modify(array(Ply))
        break        
        # |--- Settings Functionality ---|
        # |- List -|
        case 6,
            local ID = vguiClkValues()[1, number]         
            Guns:removeEntity(ID)
            Inventory = Guns:count()
            
            listUpdate()
            
            Mdl:setModel("")
            Mdl:modify(array(Ply))
        break 
        # |- Add Button -|
        case 7,
            findShipments()
        break
    }   
}

# |--- Payment Check ---|
if(moneyClk()){
    local Ply = moneyClkPlayer()
    
    local Visible = StoreFrame:isVisible(Ply)
    StoreFrame:setVisible(!Visible)
    StoreFrame:modify(array(Ply))
    
    Profit += Markup
    ShipsSold++
    Inventory = Guns:count()
    
    local Prop = propSpawn("models/props_junk/wood_crate001a.mdl", Ply:pos() + vec(0,0,75) + Ply:forward() * 75, 0)
    Prop:propFreeze(0)
    
    Guns[Selected, entity]:setPos(Prop:pos())
    Prop:propBreak()
    
    Guns:removeEntity(Selected)
    Selected = 0
    
    listUpdate()
}
